plugins {
    id 'java'
    id 'antlr'
    id 'application'
}

group = 'pl.edu.pwr'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

application {
    mainModule = 'pl.edu.pwr'
    mainClass = 'pl.edu.pwr.Main'
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // https://mvnrepository.com/artifact/org.antlr/antlr4-runtime
    antlr 'org.antlr:antlr4:4.13.2'
    implementation "org.antlr:antlr4-runtime:4.13.2"
    // https://mvnrepository.com/artifact/org.antlr/antlr4
    implementation "org.antlr:antlr4:4.13.2"
}

test {
    useJUnitPlatform()
}

def grammarPackage = "pl.edu.pwr.grammar"

tasks.generateGrammarSource {
    source = fileTree("src/main/antlr")
    outputDirectory = file("src/main/gen/${grammarPackage.replace('.', '/')}")
    arguments = ["-visitor", "-no-listener", "-package", grammarPackage, "-Dlanguage=Java"]
}

tasks.compileJava {
    dependsOn tasks.generateGrammarSource
}

sourceSets {
    main {
        java.srcDirs = ["src/main/gen", "src/main/java"]
    }
}

tasks.build {
    dependsOn tasks.generateGrammarSource
}

jar {
    from sourceSets.main.allSource
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'pl.edu.pwr.Main'
    }
    archiveBaseName = project.name + '-all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
